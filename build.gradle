apply plugin: "java"

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim().replaceAll("^v", "")
}

version = getVersionName()

repositories {
    mavenCentral()
}

dependencies {
    compile "org.projectlombok:lombok:1.16.4"
    compile "org.json:json:20140107"
    compile "org.apache.httpcomponents:httpclient:4.3.6"
    compile "org.apache.httpcomponents:httpmime:4.3.6"
    compile "org.apache.httpcomponents:httpasyncclient:4.0.2"
    compile "com.mashape.unirest:unirest-java:1.4.5"
    testCompile "junit:junit:4.12"
}

sourceSets {
    main.java.srcDir "src"
    test.java.srcDir "tests"
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes("Implementation-Title": "Respoke-Java")
        attributes("Implementation-Version": version)
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

defaultTasks 'build'
